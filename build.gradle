buildscript {
	ext {
		springBootVersion = '1.5.6.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

sourceSets {

	shared {
		groovy {
			srcDirs = ['src/test/shared/groovy']
		}

		resources {
			srcDirs = ['src/test/shared/resources']
		}

		compileClasspath += sourceSets.test.runtimeClasspath
		runtimeClasspath += sourceSets.test.runtimeClasspath
	}

	systemTest {
		groovy {
			srcDirs = ['src/test/system/groovy',]
		}
		resources {
			srcDirs = [
					'src/test/system/resources',
			]
		}

		compileClasspath += sourceSets.shared.runtimeClasspath
		runtimeClasspath += sourceSets.shared.runtimeClasspath
	}

	unitTest {
		groovy {
			srcDirs = ['src/test/unit/groovy']
		}
		resources {
			srcDirs = [
					'src/test/unit/resources',
			]
		}

		compileClasspath += sourceSets.shared.runtimeClasspath
		runtimeClasspath += sourceSets.shared.runtimeClasspath
	}

	// we need to build the shared source set
	functionTest {
		groovy {
			srcDirs = ['src/test/function/groovy']
		}
		resources {
			srcDirs = [
					'src/test/function/resources',
			]
		}

		compileClasspath += sourceSets.shared.runtimeClasspath
		runtimeClasspath += sourceSets.shared.runtimeClasspath
	}
}

task unitTest(type: Test, group: 'verification') {
	testClassesDir = sourceSets.unitTest.output.classesDir
	classpath += sourceSets.unitTest.runtimeClasspath
	outputs.upToDateWhen {false}
}

task functionTest(type: Test, group: 'verification') {
	mustRunAfter unitTest
	testClassesDir = sourceSets.functionTest.output.classesDir
	classpath += sourceSets.functionTest.runtimeClasspath
	outputs.upToDateWhen {false}
}

task systemTest(type: Test, group: 'verification') {
	testClassesDir = sourceSets.systemTest.output.classesDir
	classpath += sourceSets.systemTest.runtimeClasspath
	outputs.upToDateWhen {false}
}

test {
	dependsOn = [unitTest, functionTest]
}

idea {
	module {
		testSourceDirs += file('src/test/function/groovy')
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-amqp'
	compile 'org.codehaus.groovy:groovy-all:2.4.7'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
	testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
	testCompile 'cglib:cglib-nodep:2.2'
}
