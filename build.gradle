buildscript {
	ext {
		springBootVersion = '1.5.6.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

sourceSets {
	systemTest {
		groovy {
			srcDir 'src/test/system/groovy'
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
		}
		resources.srcDirs = ['src/test/system/resources']
	}

	unitTest {
		groovy {
			srcDir 'src/test/unit/groovy'
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
		}
		resources.srcDirs = ['src/test/unit/resources']
	}
}

configurations {
	unitTestCompile.extendsFrom testCompile
	unitTestRuntime.extendsFrom testRuntime

	systemTestCompile.extendsFrom testCompile
	systemTestCompile.extendsFrom testRuntime
}

test {
	testClassesDir = sourceSets.unitTest.output.classesDir
	classpath = sourceSets.unitTest.runtimeClasspath
}

task systemTest(type: Test, group: 'verification') {
	testClassesDir = sourceSets.systemTest.output.classesDir
	classpath = sourceSets.systemTest.runtimeClasspath
}

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-amqp'
	compile 'org.codehaus.groovy:groovy-all:2.4.7'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
	testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
	testCompile 'cglib:cglib-nodep:2.2'
}
